ext {
	info = [
		name: 'testmod-mc',
		group: 'cpup.mc.testMod',
		version: 'dev',
		dependencies: [
			'cpup.lib:cpup-lib:dev',
			'cpup.mc.lib:cpup-mc:dev'
		]
	]

	versions = [
		scala: '2.11.1',
		minecraft: '1.7.10',
		forge: '10.13.0.1208'
	]
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = 'forge'
			url = 'http://files.minecraftforge.net/maven'
		}
		maven {
			name = 'sonatype'
			url = 'https://oss.sonatype.org/content/repositories/snapshots/'
		}
	}

	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.3-SNAPSHOT'
	}
}

apply plugin: 'java'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	info.dependencies.each { dep ->
		compile dep
	}
}

apply plugin: 'scala'
dependencies.compile "org.scala-lang:scala-compiler:$versions.scala"
dependencies.compile "org.scala-lang:scala-library:$versions.scala"
dependencies.compile "org.scala-lang:scala-reflect:$versions.scala"

apply plugin: 'forge'
minecraft {
	version = "$versions.minecraft-$versions.forge"
	assetDir = 'eclipse/assets'
}

apply plugin: 'maven'

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand version: info.version, mcVersion: versions.minecraft
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

archivesBaseName = info.name
group = info.group
version = info.version